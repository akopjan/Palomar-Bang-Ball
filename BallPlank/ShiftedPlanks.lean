module

public import BallPlank.Planks

/-!
# Shifted planks through an orthonormal basis

Affine version of `Planks.exists_point_outside_planks`: the planks
`Pᵢ = {z : |ℓᵢ(z) - cᵢ| < 1}` (arbitrary centres `cᵢ`) still do not cover the ball of radius
`√n`. The proof is the same as in the text; the only change is that the quadratic form `Q`
receives the extra linear term `-2 ∑ᵢ εᵢ pᵢ⁴ cᵢ`.
-/

@[expose] public section

open scoped RealInnerProductSpace
open Classical

noncomputable section

namespace Planks

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ℝ E] [FiniteDimensional ℝ E]
variable {ι : Type*} [Fintype ι]

omit [FiniteDimensional ℝ E] in
lemma bang_shift (ℓ : ι → E →L[ℝ] ℝ) (c : ι → ℝ) (y : ι → E) (hy0 : ∀ i, ℓ i (y i) ≠ 0)
    (hsym : ∀ i j, ((ℓ i (y i))⁻¹) ^ 3 * ℓ i (y j) = ((ℓ j (y j))⁻¹) ^ 3 * ℓ j (y i)) :
    ∃ ε : ι → Bool, ∀ k,
      1 ≤ |ℓ k (∑ i, (sgn (ε i) * (ℓ i (y i))⁻¹) • y i) - c k| := by
  set p : ι → ℝ := fun i => (ℓ i (y i))⁻¹ with hp
  let M : ι → ι → ℝ := fun i j => p i ^ 4 * p j * ℓ i (y j)
  have hM : ∀ i j, M i j = M j i := by
    intro i j
    have := hsym i j
    calc p i ^ 4 * p j * ℓ i (y j) = p i * p j * (p i ^ 3 * ℓ i (y j)) := by ring
      _ = p i * p j * (p j ^ 3 * ℓ j (y i)) := by rw [this]
      _ = _ := by ring
  let Q : (ι → Bool) → ℝ := fun ε =>
    ∑ i, ∑ j, sgn (ε i) * sgn (ε j) * M i j - 2 * ∑ i, sgn (ε i) * (p i ^ 4 * c i)
  obtain ⟨ε, -, hε⟩ := Finset.exists_max_image Finset.univ Q Finset.univ_nonempty
  refine ⟨ε, fun k => ?_⟩
  have hle := hε (Function.update ε k (!ε k)) (Finset.mem_univ _)
  have hs' : ∀ i, sgn (Function.update ε k (!ε k) i) = if i = k then -sgn (ε k) else sgn (ε i) := by
    intro i
    by_cases h : i = k
    · subst h; simp only [Function.update_self, ite_true]; cases ε i <;> simp [sgn]
    · simp [h]
  have key := flip_quad M hM (fun i => sgn (ε i)) (fun i => sgn (Function.update ε k (!ε k) i)) k
    hs'
  have hlin : ∑ i, sgn (Function.update ε k (!ε k) i) * (p i ^ 4 * c i) =
      ∑ i, sgn (ε i) * (p i ^ 4 * c i) - 2 * sgn (ε k) * (p k ^ 4 * c k) := by
    have e : ∀ i, sgn (Function.update ε k (!ε k) i) * (p i ^ 4 * c i) =
        sgn (ε i) * (p i ^ 4 * c i) + (if i = k then -2 * sgn (ε k) * (p k ^ 4 * c k) else 0) := by
      intro i; rw [hs']; by_cases h : i = k
      · subst h; simp; ring
      · simp [h]
    simp only [e, Finset.sum_add_distrib, Finset.sum_ite_eq', Finset.mem_univ, ite_true]
    ring
  have hz : ℓ k (∑ i, (sgn (ε i) * p i) • y i) = ∑ j, sgn (ε j) * p j * ℓ k (y j) := by
    simp [map_sum, map_smul]
  have hrow : ∑ j, sgn (ε j) * M k j = p k ^ 4 * ℓ k (∑ i, (sgn (ε i) * p i) • y i) := by
    rw [hz, Finset.mul_sum]
    exact Finset.sum_congr rfl fun j _ => by simp only [M]; ring
  have hkk : M k k = p k ^ 4 := by
    simp only [M, hp]
    field_simp [hy0 k]
  have hsk : sgn (ε k) ^ 2 = 1 := by unfold sgn; split_ifs <;> norm_num
  have hpk : 0 < p k ^ 4 := by
    have : p k ≠ 0 := inv_ne_zero (hy0 k)
    positivity
  change ∑ i, ∑ j, _ - 2 * ∑ i, _ ≤ ∑ i, ∑ j, _ - 2 * ∑ i, _ at hle
  rw [key, hlin, hrow, hkk, hsk] at hle
  have h1 : 1 ≤ sgn (ε k) * (ℓ k (∑ i, (sgn (ε i) * p i) • y i) - c k) := by
    nlinarith
  show 1 ≤ |ℓ k (∑ i, (sgn (ε i) * p i) • y i) - c k|
  set w := ℓ k (∑ i, (sgn (ε i) * p i) • y i) - c k
  cases hεk : ε k <;> simp only [sgn, hεk, ite_true, Bool.false_eq_true, ite_false] at h1
  · linarith [neg_le_abs w]
  · linarith [le_abs_self w]

/-- Shifted version: for an orthonormal family `x` with `ℓᵢ(xᵢ) = 1` and arbitrary reals `cᵢ`,
some point of norm `≤ √|ι|` satisfies `|ℓᵢ(z) - cᵢ| ≥ 1` for all `i`. -/
theorem exists_point_outside_shifted_planks (ℓ : ι → E →L[ℝ] ℝ) (c : ι → ℝ) (x : ι → E)
    (hx : Orthonormal ℝ x) (h : ∀ i, ℓ i (x i) = 1) :
    ∃ z : E, ‖z‖ ≤ Real.sqrt (Fintype.card ι) ∧ ∀ i, 1 ≤ |ℓ i z - c i| := by
  obtain ⟨y, hyS, hmin, hF⟩ := exists_minimizer ℓ x hx h
  have hy0 : ∀ i, ℓ i (y i) ≠ 0 := fun i hi => by
    have := hyS.2 i
    rw [hi] at this
    norm_num at this
  have hsym : ∀ i j, ((ℓ i (y i))⁻¹) ^ 3 * ℓ i (y j) = ((ℓ j (y j))⁻¹) ^ 3 * ℓ j (y i) :=
    fun i j => if hij : i = j then by subst hij; rfl else stationary ℓ y hyS hmin hF i j hij
  obtain ⟨ε, hε⟩ := bang_shift ℓ c y hy0 hsym
  refine ⟨_, ?_, hε⟩
  rw [← Real.sqrt_sq (norm_nonneg _)]
  apply Real.sqrt_le_sqrt
  rw [norm_sq_sum y hyS.1]
  calc ∑ i, (sgn (ε i) * (ℓ i (y i))⁻¹) ^ 2 = objF ℓ y := by
        unfold objF
        refine Finset.sum_congr rfl fun i _ => ?_
        have : sgn (ε i) ^ 2 = 1 := by unfold sgn; split_ifs <;> norm_num
        rw [mul_pow, this, one_mul]
    _ ≤ _ := hF

end Planks

end
